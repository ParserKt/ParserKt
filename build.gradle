import static System.getenv

plugins {
  id "kotlin-multiplatform" version "1.3.61"
  id "maven-publish"
  id "org.jetbrains.dokka" version "0.10.1"
  id 'com.github.johnrengelman.shadow' version "5.2.0"
}

import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

allprojects {
  group "org.parserkt"
  version "1.0"

  apply plugin: "kotlin-multiplatform"

  kotlin {
    jvm(); js()
  }

  repositories { jcenter() }
  ext.kotlin = { name -> return "org.jetbrains.kotlin:kotlin-$name:1.3.61" }

  dependencies {
    commonMainImplementation kotlin("stdlib-common")
    jvmMainImplementation kotlin("stdlib")
    jsMainImplementation kotlin("stdlib-js")

    commonTestImplementation kotlin("test-common")
    commonTestImplementation kotlin("test-annotations-common")

    jvmTestImplementation kotlin("test-junit")
    jsTestImplementation kotlin("test-js")
  }
}

//// == Kotlin & Libs ==
dokka {
  def parserKtProjects = rootProject.subprojects.asList().collect { it.name }
  outputFormat = "html"
  outputDirectory = "$buildDir/dokka"
  subProjects = parserKtProjects
}

//// == Utilities and Sub-library ==
dependencies {
  commonMainImplementation project("parserkt-util")
}

//// == Publish ==
allprojects {
  apply plugin: "maven-publish"
}

task shadowJar(type: ShadowJar, dependsOn: [jvmJar]) {
  baseName = "ParserKt"
  manifest {}

  from jvmJar.archiveFile
  from project("parserkt-util").jvmJar.archiveFile
  from project("parserkt-ext").jvmJar.archiveFile
  //configurations = [project.configurations.jvmRuntimeClasspath]
}
