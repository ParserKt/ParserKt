import static System.getenv

plugins {
  id "kotlin-multiplatform" version "1.3.61"
  id "maven-publish"
  id "org.jetbrains.dokka" version '0.10.1'
}

allprojects {
  group "org.parserkt"
  version "0.9"

  repositories { jcenter() }

  ext.kotlin = { name -> return "org.jetbrains.kotlin:kotlin-$name:1.3.61" }
}

subprojects {
  apply plugin: "kotlin-multiplatform"

  kotlin {
    jvm(); js()
  }

  dependencies {
    commonMainImplementation kotlin("stdlib-common")
    jvmMainImplementation kotlin("stdlib")
    jsMainImplementation kotlin("stdlib-js")
  }
}

//// == Kotlin & Libs ==
kotlin {
  jvm(); js()
}

dokka {
  def parserKtProjects = rootProject.subprojects.asList().collect { it.name }
  outputFormat = "html"
  outputDirectory = "$buildDir/dokka"
  subProjects = parserKtProjects
}

dependencies {
  commonMainImplementation kotlin("stdlib-common")
  jvmMainImplementation kotlin("stdlib")
  jsMainImplementation kotlin("stdlib-js")

  commonTestImplementation kotlin("test-common")
  commonTestImplementation kotlin("test-annotations-common")

  jvmTestImplementation kotlin("test-junit")
  jsTestImplementation kotlin("test-js")
}

//// == Utilities sub-library ==
dependencies {
  commonMainImplementation project("parserkt-util")
}

//// == Publish ==
allprojects {
  apply plugin: "maven-publish"
}
