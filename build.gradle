import static System.getenv

plugins {
  id "kotlin-multiplatform" version "1.3.61"
  id "org.jetbrains.dokka" version '0.10.1'
  id "maven-publish"
}

allprojects {
  group "org.parserkt"
  version "0.9"

  apply plugin: "kotlin-multiplatform"

  kotlin {
    jvm(); js()
  }

  repositories { jcenter() }
  ext.kotlin = { name -> return "org.jetbrains.kotlin:kotlin-$name:1.3.61" }

  dependencies {
    commonMainImplementation kotlin("stdlib-common")
    jvmMainImplementation kotlin("stdlib")
    jsMainImplementation kotlin("stdlib-js")

    commonTestImplementation kotlin("test-common")
    commonTestImplementation kotlin("test-annotations-common")

    jvmTestImplementation kotlin("test-junit")
    jsTestImplementation kotlin("test-js")
  }
}

//// == Kotlin & Libs ==
dokka {
  def parserKtProjects = rootProject.subprojects.asList().collect { it.name }
  outputFormat = "html"
  outputDirectory = "$buildDir/dokka"
  subProjects = parserKtProjects
}

//// == Utilities and Sub-library ==
dependencies {
  commonMainImplementation project("parserkt-util")
}

//// == Publish ==
allprojects {
  apply plugin: "maven-publish"
}
