import static System.getenv

plugins {
  id "kotlin-multiplatform" version "1.3.61"
  id 'com.github.johnrengelman.shadow' version "5.2.0"
  id "maven-publish"; id "signing"
  id "io.codearte.nexus-staging" version "0.21.2"
  id "org.jetbrains.dokka" version "0.10.1"
}

import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

allprojects {
  group "fun.parser"
  version "2.5"

  apply plugin: "kotlin-multiplatform"
  kotlin { jvm(); js() }

  compileKotlinJs {
    kotlinOptions.outputFile = "${buildDir}/js/${project.name}.js"
    kotlinOptions.moduleKind = "umd"
    kotlinOptions.sourceMap = true
    kotlinOptions.metaInfo = true
  }

  repositories { jcenter() }
  ext.kotlin = { name -> return "org.jetbrains.kotlin:kotlin-$name:1.3.61" }

  dependencies {
    commonMainImplementation kotlin("stdlib-common")
    jvmMainImplementation kotlin("stdlib")
    jsMainImplementation kotlin("stdlib-js")

    commonTestImplementation kotlin("test-common")
    commonTestImplementation kotlin("test-annotations-common")

    jvmTestImplementation kotlin("test-junit")
    jsTestImplementation kotlin("test-js")
  }
}

//// == Kotlin & Libs ==
dokka {
  def parserKtProjects = rootProject.subprojects.asList().collect { it.name }
  outputFormat = "html"
  outputDirectory = "$buildDir/dokka"
  subProjects = parserKtProjects
}

dependencies {
  commonMainImplementation project("parserkt-util")
}

//// == Publish ==
description = "Naive one-pass recursive descent, scannerless parser framework for Kotlin"

task shadowJar(type: ShadowJar, dependsOn: [jvmJar]) {
  baseName = "ParserKt"
  manifest {}

  from jvmJar.archiveFile
  from project("parserkt-util").jvmJar.archiveFile
  from project("parserkt-ext").jvmJar.archiveFile
  //configurations = [project.configurations.jvmRuntimeClasspath]
}

def getprop(String name) { return rootProject.properties[name] }

allprojects {
  apply plugin: "maven-publish"
  publishing {
    repositories {
      maven { name = "GitHubPackages"
        url = uri("https://maven.pkg.github.com/ParserKt/ParserKt")
        credentials { username = getenv("USERNAME"); password = getenv("GITHUB_TOKEN") }
      }
      maven { name = "Sonatype"
        url = uri("https://oss.sonatype.org/service/local/staging/deploy/maven2")
        credentials { username = getprop("nexusUsername"); password = getprop("nexusPassword") }
      }
    }
    publications {}
  } //publishing
}

signing {
  required true
  sign(publishing.publications)
}
